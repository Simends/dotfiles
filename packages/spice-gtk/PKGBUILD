# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk
pkgver=0.40
pkgrel=1
pkgdesc="GTK+ client library for SPICE"
arch=('x86_64')
url="https://www.spice-space.org/"
group=('custom')
license=('LGPL2.1')
depends=(
    'gst-plugins-base'
    'gst-plugins-good'
    'libcacard'
    'libgtk-3.so'
    'libjpeg.so'
    'libopus.so'
    'phodav'
    'usbredir'
)
makedepends=(
    'gobject-introspection'
    'meson'
    'spice-protocol'
    'usbutils'
    'vala'
    'wayland-protocols'
)
provides=("spice-glib=$pkgver" "spice-gtk3=$pkgver")
replaces=('spice-glib' 'spice-gtk3')
source=("https://www.spice-space.org/download/gtk/$pkgname-$pkgver.tar.xz")
sha256sums=('23f5ff7fa80b75647ce73cda5eaf8b322f3432dbbb7f6f3a839634618adbced3')

build() {
  artix-meson $pkgname-$pkgver build \
    -Dpolkit=disabled
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

